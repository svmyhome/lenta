plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
    id("io.freefair.lombok") version "8.11"
}

def restAssuredVersion = "5.5.0",
    allureRestAssured = "2.29.1",
    allureVersion = "2.29.0",
    selenideVersion = "7.5.1",
    appiumVersion = "9.3.0",
    junitVersion = "5.11.2",
    slf4jVersion = "2.0.16"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }

}


repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            'commons-io:commons-io:2.6',
            'org.aeonbits.owner:owner:1.0.12',
            'org.assertj:assertj-core:3.26.3',
            "com.codeborne:selenide:$selenideVersion",
            'io.github.bonigarcia:webdrivermanager:5.9.2',
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            'io.qameta.allure:allure-junit5:2.29.1',
            'io.qameta.allure:allure-java-commons:2.14.0',
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureRestAssured",
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.18.2',
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "io.appium:java-client:$appiumVersion"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "passed", "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }

    systemProperties(System.getProperties())
}

tasks.register('api_tests', Test) {
    group = 'Verification'
    description = 'Runs API tests'
    useJUnitPlatform {
        includeTags("api")
    }
}

tasks.register('ui_tests', Test) {
    group = 'Verification'
    description = 'Runs UI tests'
    useJUnitPlatform {
        includeTags("ui")
    }
}

tasks.register('all_tests', Test) {
    group = 'Verification'
    description = 'Runs all tests (API and UI)'
    useJUnitPlatform {
        includeTags("api", "ui")
    }
}

tasks.register('android_tests', Test) {
    group = 'Verification'
    description = 'Runs Android tests'
    useJUnitPlatform {
        includeTags("android")
    }
}